"Timeout in the Wasteland" by "Feneric"

The story headline is "Survival in Unfortunate Times".
The story creation year is 2020.
The release number is 3.
The story genre is "Science Fiction".
After printing the banner text, say "(First-time users should type 'about'.)"

Use American Dialect.
Use serial comma.
Release along with cover art.
Release along with an interpreter.

day-count is a number which varies.
xeno is a number which varies. old_xeno is a number which varies.
yako is a number which varies. old_yako is a number which varies.
lights-on-flag is number which varies.
have-filled-water-flag is a number which varies.
splicer-run-today-flag is a number which varies.
water-tested-today-flag is a number which varies.
incubator-filled-today-flag is a number which varies.
garden-dead-flag is a number which varies.
days-since-garden-filled is a number which varies.
workbench-tested-flag is a number which varies.
alternate-water-flag is a number which varies.
have-filled-canteen is a number which varies.
number-of-ant-bites is a number which varies.
thoughts is a list of text that varies.
thought-number is a number which varies.
number-of-thoughts is a number which varies.
rooms-away is a number which varies.
A person has a number called hunger.
A person has a number called thirst.
A person has a number called sickness.
A thing has some text called printing. The printing of a thing is usually "blank".

To advance-time (amount - time):
	let the target-time be the time of day plus the amount minus 1 minute;
	while the time of day is not the target-time:
		follow the turn sequence rules.

When play begins:
	now thoughts is {"Life used to be so much more complicated. It's hard to believe now that I used to have a job that I had to commute to, and that there were other people there that I had to talk with. It's been awhile since I've had to talk. It's been a real long while since I've had to drive (or since I've even seen a car, for that matter).", "When I envisioned the future, it was somehow never like this.", "As I think back, I never really prepared for this sort of situation in any meaningful way.", "Who would have thought that with all the people out there preparing for the eschaton, be it Armageddon, Ragnar√∂k, Helter-Skelter, or what-have-you, someone like me would actually be the one to survive?", "I'm an ordinary plant geneticist without any special combat skills. I wasn't brought up by fighting monks. I wasn't taught how to survive alone in the wilderness. I've never even been camping before this. I doubt I could even light a fire on my own. What am I doing here?", "Of course, I'm not the only survivor. I can't be.", "There must be others out there. I just haven't seen them and have no means to contact them.", "Now I'm out here alone.", "Or more accurately I wish I were alone. I still have to worry about Them.", "So long as I stay indoors at night I think I'm safe. They hate the light.", "I'm alone.", "I used to know other people", "I used to talk with other people.", "I don't know if I remember how to talk. It's been so long.", "The world used to be not such a bad place. I mean, there were lots of problems all the time, and lots of people had impossible situations. Still, it was better than this.", "It's funny thinking back to a time when I didn't have to test my drinking water everyday. Of course, knowing what I know now, I probably should have been testing it, even back then.", "I remember when food literally grew on trees in some places.", "Maybe the air is cleaner now. It don't have any way of measuring it for sure.", "Back when people first started getting sick, there were some efforts to slow the spread. These efforts were mostly haphazard and disorganized. Generally greedy people were more interested in their own personal stockpiles of cash and/or supplies than other people's lives.", "For a lot of people the disruption to their lives seemed like a timeout. For me that timeout never ended, and I don't know if my normal life will ever resume.", "I remember losing so much. I remember that now better than ever having anything to begin with.", "I've never been a people person. That's probably for the best.", "Everything's good in the world. I'm making things a little better every day.", "Once upon a time there was a geneticist who through a stroke of dumb luck survived the apocalypse. This geneticist couldn't find any of the really cool equipment needed for the task of rebuilding the world. All the cutting-edge stuff stopped working. There were some older bits and pieces of things that were salvageable though.[paragraph break]I don't think any publishers would take my story. Even if there were still publishers.", "Ever notice how when you're really focused on one problem, another one sneaks up and bites you? No? We didn't either.", "They always used to say that hindsight is 2020. That's even more true now.", "People got so tied up in their tribes. No one is perfect, and that's even more true for groups of people. No one was holding their own tribes accountable for their mistakes and misdeeds.", "Evolution is picking the best of the best repeatedly. Choosing the least bad of the least bad repeatedly leads to de-evolution. Some cycles need to be broken.", "Sometimes I wish I weren't alone all the time. Then sometimes I really remember what it was like trying to live around large groups of people. There should be a happy medium.", "I remember a week or so after the world broke I managed to get away from the city. I was able to get out to what was left of my cousin's farm. He and his wife had a nice working farm. They had numerous vegetables and livestock. I was much too late to help them of course.", "I wasn't surprised that They killed my cousin and his wife. I was surprised that They also went after all the livestock.", "Out of all the animals my cousin kept, I could only find one survivor: a young ferret. Its family had been killed by Them, too. My cousin raised ferrets to keep the farm clear of rodents.", "The ferret came with me. I don't know what my cousin called him. I renamed him Fangtastic. We brought lots of seeds with us, too.", "Today the seeds of the seeds of the seeds of the seeds of (I forget how many levels deep now) my cousin's plants are still feeding me. Maybe with my work they'll be able to help others, too.", "Fangtastic was my constant companion while I built up the base. He fed himself on the rodents that would otherwise attack my garden. He'd also just hang around with me, and he really seemed to listen. I know rationally he didn't understand me, but he always gave the impression that he was at least trying.", "I couldn't do anything for him in the end. When he passed, a part of me did, too. I buried him in the corner of the garden under the biggest stone I could move. I haven't had any reason to talk since then.", "Now I feel so very alone.", "Who am I kidding? There probably is no one else left. These things I do are probably pointless. There's nothing to be gained for all my effort.", "Sometimes there is no hope.", "I must continue. I must persevere. How we behave matters most when we're not witnessed and only we know what we're doing.", "I will help the wasteland support life again.", "What I do matters, at least right here and right now.", "I will succeed."};
	now the number-of-thoughts is the number of entries in thoughts;
	now the day-count is 1;
	now the days-since-garden-filled is 1;
	now the right hand status line is "Day [day-count] [time of day]";
	now the story viewpoint is first person singular;
	say "I enter my lab, ready to face another day. The end of civilization isn't quite the end of the world, so long as there are people like me who are willing to preserve little islands of it, at least. Plus achieving my lofty goals could even help with its restoration. How's that for motivation? It's not like I have much of a choice though if it isn't enough. Based on my remaining inventory and how long it takes my various plants to grow and knowing how long they'll sustain me, my calculations last night show that if I don't manage to succeed within the next five days, my long term prospects are extremely grim.[paragraph break][italic type]As important as the work is, it's often fairly repetitious and the thoughts and memories keep coming back to me unbidden. When I'm out in the wasteland, my mind wanders as much as my body.[roman type][paragraph break]".

The interior spaces is a region.
The floor is a backdrop in the interior spaces. The description is "It's just an ordinary tile floor.".
The ceiling is a backdrop in the interior spaces. The description is "It's a basic ceiling with embedded LED lights.".
The wall is a backdrop in the interior spaces. Understand "walls" as the plural of wall. The description is "It's a plain off-white wall. There are no decorations on it.".
The exterior spaces is a region.
Instead of listening when the location is in the exterior spaces:
	say "It's fairly quiet right now aside from some howling of the wind. At least I think it's the wind.".
The sun is a backdrop in the exterior spaces.
The ground is a backdrop in the exterior spaces. Understand "wasteland/-- ground" and "wasteland/-- soil" as the ground. The description is "The wasteland ground is better than desert, but not by a lot. Some wild plants manage to live in it, but I don't see any nearby.".
The sky is a backdrop in the exterior spaces. The description is "The sky here is never cloudy.".
The dangerous spaces is a region in the exterior spaces.
The grue is a backdrop in the dangerous spaces. Understand "grues" and "them" as the plural of grue. The description is "I don't see any of Them here.".
Instead of doing anything to the grue:
	try examining the grue.
The LEDs are a backdrop in the interior spaces. Understand "embedded/led/-- light", "embedded/led/-- lights", "embedded/-- led", "embedded/-- leds", "light-emitting diode", and "light-emitting diodes" as the LEDs. The description is "The LEDs are extremely efficient and can run for around an hour or so using energy harvested from the sun during the day. [first time]Most of the electricity is needed for the fence. [only]They are set in the ceiling and it's not possible (or desirable) to mess with them. They are currently [if lights-on-flag is 0]off[otherwise]on[end if].".
Before switching on or switching off the LEDs:
	say "They're automatic based on light levels. There's no way to manually turn them on or off.".

The Laboratory is a room. It is in the interior spaces. The description is "My laboratory is extremely humble with the exception of two precious devices: my incubator and my plant gene splicer. Each is bulky and heavy, more like a piece of furniture than a precise scientific instrument. Between them I have an old workbench which is [if workbench-tested-flag is 0]probably [end if]strong enough to stand on[if workbench-tested-flag is 0], but I've never really had the reason to climb onto it[end if]. The outer compound is a short ladder trip up to the surface.".

The ladder is scenery in the Laboratory. Understand "ladder" as the ladder. The description is "The ladder is fastened to the wall and leads up to the outer compound."

Instead of listening when the location is the laboratory:
	say "There's a slight drone caused by the incubator, but it's nearly imperceptible.".

The gene splicer is a device in the Laboratory. It is scenery. It is switched off. Understand "plant gene splicer" and "gene/plant/-- splicer" as the gene splicer. The description is "The plant gene splicer is a little taller and wider than me and about five times my weight.[paragraph break]It is able to extract genetic samples from two different plants and splice them together, ultimately giving me a seed that I can plant and observe. It is a slow and complicated process, but I have gone through it so many times I can now almost splice plants in my sleep.";

To say incubator-contents:
	repeat with current-booth running through booths that are part of incubator:
		if something (called the current-item) is in current-booth:
			say "There is a [current-item] in [current-booth]. [run paragraph on]";
		otherwise:
			say "There is nothing in [current-booth]. [run paragraph on]".

Instead of switching on the gene splicer:
	say "I don't need to mess with that now. I can 'splice first plant with second plant' or even just 'splice slot # with slot #' to run the whole procedure once I've decide which ones to combine.";
	rule fails.

The incubator is a device in the Laboratory. It is scenery. It is switched on. Understand "plant/seed/-- incubator" and "incubator/-- basin" as the incubator. The description is "The incubator enables me to grow plants quickly in a controlled environment. With it switched on, seeds will sprout and plants will grow nearly twice as fast as they would ordinarily, and it keeps them safe from pests. I do have to fill a basin on it with water once a day to keep the plants alive[if incubator-filled-today-flag is 0], and in fact I still have to do so today[end if].[paragraph break]It looks a little like a salad bar with five isolated glass covered sections, each one labeled with a number. [incubator-contents][paragraph break]";

Instead of switching off the incubator:
	say "I can't interrupt my experiments when I'm so close to a solution.";
	rule fails.

Instead of putting when the second noun is the incubator:
	try inserting noun into incubator.

Before inserting into when the the second noun is the incubator:
	if the noun is water or the noun is the canteen:
		if the location is not the laboratory:
			say "The incubator is not here." instead;
			rule fails;
		otherwise if the canteen is not touchable:
			say "I need the canteen to fill the incubator basin with water." instead;
			rule fails;
		otherwise if water is not in the canteen:
			say "There is no water in the canteen." instead;
			rule fails;
		otherwise:
			say "I fill the incubator basin with water from the canteen.";
			check-canteen-level;
			now incubator-filled-today-flag is 1;
			rule succeeds;

A booth is a kind of container. It is closed, openable, transparent scenery. The carrying capacity of a booth is 1. Understand "booth" and "slot" and "section" as a booth. Understand "booths" and "slots" and "sections" as the plural of booth.
slot 1 is a booth. It is part of the incubator. Understand "slot 1" and "first slot" as slot 1. slot 2 is a booth. It is part of the incubator. Understand "slot 2" and "second slot" as slot 2. slot 3 is a booth. It is part of the incubator. Understand "slot 3" and "third slot" as slot 3. slot 4 is a booth. It is part of the incubator. Understand "slot 4" and "fourth slot" as slot 4. slot 5 is a booth. It is part of the incubator. Understand "slot 5" and "fifth slot" as slot 5. slot 0 is a booth.

To decide which booth is first-empty-slot:
	repeat with current-booth running through booths that are part of incubator:
		if nothing is in current-booth:
			decide on current-booth;
	decide on slot 0.

Instead of taking when the noun is a booth and something (called the current-item) is in the noun:
	try taking the current-item;

After examining a booth (called the current-booth):
	if something (called current-item) is in the current-booth:
		try examining current-item;

Understand "dig up/out/-- [something]" as taking.

Instead of taking when the noun is planted:
	if the holder of noun is closed:
		try opening the holder of noun;
	now the noun is not planted;
	say "I dig out the [noun] to make room for a new experiment.";
	move noun to player;
	rule succeeds;

The bench is an enterable supporter in the Laboratory. It is scenery. The description is "It's a simple workbench made of wood." Understand "bench", "workbench", "wooden bench", and "wooden workbench" as the bench.

Instead of climbing bench:
	try entering bench.
Understand "climb up/onto/on/-- [something]" as climbing.

Instead of entering the bench:
	say "I climb onto the bench.[first time] It looks like it is strong enough for me to stand on after all![only]";
	now workbench-tested-flag is 1;
	rule succeeds.

Understand "get down" as exiting when the location is the Laboratory.

To say describe-current-experiments:
	say "My current experiments are:[paragraph break]";
	repeat with current-booth running through booths that are part of incubator:
		if an experiment (called the current-experiment) is in current-booth:
			say "        In [current-booth] I have a [current-experiment].".

To say number-of-days-left:
	let days-left be 5 - day-count;
	if days-left is 0:
		say "today";
	otherwise if days-left is 1:
		say "by tomorrow";
	otherwise:
		say "within the next [days-left in words] days (not counting today)".

The log book is on the bench. The description is "The full log of all my soybean experiments going back to the beginning. In its own way it's even more valuable than my incubator and gene splicer. I don't have it memorized and would have to read it to get more information.". The printing is "I have the results of over 2,300 experiments carefully documented in my log. Most were failures, but many were steps on the road to success. I now have three plants that I am certain contain all the elements I need in order to successfully grow high-yield soybeans out in the wasteland: I've got a plant that can survive its tortuous sunlight, I've got a plant that can thrive with very little moisture, and I've got a plant that produces nearly twice the normal amount of beans. If I can successfully combine these three plants I will be able to turn the wasteland surrounding me into productive farmland. I'm running out of time in which to make it happen, though. If I'm not successful [number-of-days-left], I doubt I'll be able to make it long term, let alone create an environment hospitable to humans again. From past experience my best guess is to try combining the hardy plant with the drought-resistant plant first.[paragraph break][describe-current-experiments]".

A size is a kind of value. The sizes are small, medium, and large.

A canister is a kind of open unopenable container. A canister has a size. A canister has a number called percent full. Percent full is usually 100. Understand "canister" and "can" as canister. Understand "canisters" or "cans" as the plural of canister. Understand the size property as describing a canister. The carrying capacity of a canister is 0. It is edible.

Instead of putting when the second noun is a canister (called target):
	try inserting noun into target.
Instead of inserting into a canister (called target):
	say "That would mess up the [target]. I can't afford to waste any.".

Instead of closing a canister (called target):
	say "Unfortunately the lid for the [target] got lost years ago. There's no way to close it now.".

To say canister-level (can - canister):
	if percent full of can < 1:
		say "It's empty.";
	otherwise if percent full of can < 11:
		say "It is nearly empty.";
	otherwise if percent full of can < 35:
		say "It's around a quarter full.";
	otherwise if percent full of can < 65:
		say "It's around half full.";
	otherwise if percent full of can < 85:
		say "It's around three-quarters full.";
	otherwise if percent full of can < 95:
		say "It's nearly full.";
	otherwise:
		say "It looks full.".

After printing the name of a canister while not inserting, searching, examining, or removing:
	omit contents in listing.

The borax is a large canister on the bench. The indefinite article is "some". The description of it is "A large open canister of borax. [canister-level borax]".
Does the player mean eating the borax: it is unlikely.

The ant poison is nowhere. The indefinite article is "some". It is edible. The description is "It's a small amount of sticky, viscous liquid. It's cloudy but mostly transparent."
Does the player mean eating the ant poison: it is unlikely.

The potability tester is on the bench. It is a device. It is switched off. Understand "potability/-- tester", "protruding/-- sensor", and "small/-- readout" as the potability tester. The description is "It's a rectangular device a bit over a foot long with a protruding sensor and a small readout. Basically all one needs to do is bring it to the source of water to be tested and turn it on. It produces results almost instantaneously.".

Instead of putting when the noun is the potability tester and the second noun is the canteen:
	try inserting the potability tester into the canteen.
Instead of inserting when the noun is the potability tester and the second noun is the canteen:
	say "It is a little too large to fit.";
	rule fails.

Instead of switching on the potability tester:
	if the location is the Oasis:
		say "It almost immediately beeps. The readout says 'safe'. I turn it back off.";
		now water-tested-today-flag is 1;
		advance-time 3 minutes;
		rule succeeds;
	otherwise:
		say "There's no point turning it on here. I'm nowhere close to the water.";
		rule fails.

An experiment is a kind of thing. Understand "experiment" and "soybean experiment" and "soybean plant experiment" as experiment. Understand "experiments" and "soybean experiments" and "soybean plant experiments" as the plural of experiments. An experiment has a number called days old. An experiment can be planted. An experiment is usually not planted. Understand the planted property as describing an experiment.

A plant is a kind of experiment. Understand "experimental/-- plant" and "experimental/-- soybean plant" as plant. Understand "plants" and "soybean plants" as the plural of plant. A plant can be thriving. A plant is usually not thriving. Understand the thriving property as describing a plant. The printed name is "experimental soybean plant". The description is "I can't tell anything about the experimental soybean plant yet; it needs to establish itself first."

The hardy soybean plant is a plant. It is in slot 2. It is planted. It is thriving. days old is 5. The printed name is "hardy soybean plant". The description is "This soybean plant is my champion for surviving in the punishing environment of the wasteland. It is fairly low-yield and can't thrive with the amount of rain available to it in the wasteland, but it can handle the intense sun and poor soil."
The drought-resistant soybean plant is a plant. It is in slot 3. It is planted. It is thriving. days old is 5. The printed name is "drought-resistant soybean plant". The description is "This soybean plant is my champion for thriving with the limited amount of rain available to it in the wasteland, but it can't handle the intense sun and poor soil and doesn't have a great yield."
The high-yield soybean plant is a plant. It is in slot 1. It is planted. It is thriving. days old is 5. The printed name is "high-yield soybean plant". The description is "This soybean plant can't deal with either the wasteland's tortuous environment or lack of rain, but it has the best yield of all of my experiments. If I could get a plant with even half its yield to survive in the wasteland, I could produce enough food out here to support a whole settlement."
The hardy drought-resistant soybean plant is a plant. It is nowhere. It is thriving.
Before examining the hardy drought-resistant soybean plant:
	now the printed name is "hardy drought-resistant soybean plant";
	now the description is "This plant looks like it can survive out in the wasteland. I just need to increase its yield.".
The hardy high-yield soybean plant is a plant. It is nowhere. It is not thriving.
Before examining the hardy high-yield soybean plant:
	now the printed name is "hardy high-yield soybean plant";
	now the description is "This plant definitely won't survive in the wasteland. We'll see if it can even survive in the incubator.".
The drought-resistant high-yield soybean plant is a plant. It is nowhere. It is not thriving.
Before examining the drought-resistant high-yield soybean plant:
	now the printed name is "drought-resistant high-yield soybean plant";
	now the description is "This plant definitely won't survive in the wasteland. We'll see if it can even survive in the incubator.".
The hardy drought-resistant high-yield soybean plant is a plant. It is nowhere. It is thriving.
Before examining the hardy drought-resistant high-yield soybean plant:
	now the printed name is "hardy drought-resistant high-yield soybean plant";
	now the description is "This is it! This is the plant I've been working to breed all this time. It can survive in the wasteland and still produce a useful amount of food. I'll be able to start cloning it and spreading it out into the wasteland immediately.".
After examining the hardy drought-resistant high-yield soybean plant:
	if garden-dead-flag is 1:
		say "It's too bad my garden died on me and I won't survive long enough to see these plants grow.";
		end the story saying "You Lose";
	otherwise:
		end the story finally saying "You Win";
		if days-since-garden-filled > 0 or water-tested-today-flag is 0 or alternate-water-flag is 0 or sickness of player > 0 or thirst of player > 50 or hunger of player > 50:
			say "There are a few things I could have done better. ";
			if days-since-garden-filled > 0:
				say "I should have taken better care of my garden. ";
			if water-tested-today-flag is 0:
				say "I probably should have been more cautious with testing my water source. ";
			if alternate-water-flag is 0:
				say "I definitely should have located a backup water source. ";
			if sickness of player > 0:
				say "I should have taken better care of myself and avoided getting sick. ";
			if thirst of player > 50:
				say "I was not drinking enough. ";
			if hunger of player > 50:
				say "I was not eating enough. ".
The sickly soybean plant is a plant. It is nowhere.
Before examining the sickly soybean plant:
	now the printed name is "sickly soybean plant";
	now the description is "This plant looks unwell; it is likely to die within a day or two.".

Instead of doing something other than examining or mixing with the hardy soybean plant: say "That's one of my most successful experiments. It has the ability to survive out in the wastelands with only a tiny bit of additional water provided to it. I need it for future splicing and don't want to harm it.".
Instead of doing something other than examining or mixing with slot 1: say "That contains one of my most successful experiments. It has the ability to survive out in the wastelands with only a tiny bit of additional water provided to it. I need it for future splicing and don't want to harm it.".
Instead of doing something other than examining or mixing with the drought-resistant soybean plant: say "That's one of my most successful experiments. It has the ability to survive even with very little water. I need it for future splicing and don't want to harm it.".
Instead of doing something other than examining or mixing with slot 2: say "That contains one of my most successful experiments. It has the ability to survive even with very little water. I need it for future splicing and don't want to harm it.".
Instead of doing something other than examining or mixing with the high-yield soybean plant: say "That's one of my most successful experiments. It produces significantly more beans than the typical plant. I need it for future splicing and don't want to harm it.".
Instead of doing something other than examining or mixing with slot 3: say "That contains one of my most successful experiments. It produces significantly more beans than the typical plant. I need it for future splicing and don't want to harm it.".

A seed is a kind of edible experiment. Understand "seed" and "soybean seed" and "soybean" as seed. Understand "seeds" and "soybean seeds" and "soybeans" as the plural of seeds. A seed has a plant called adult form. A seed can be germinating. A seed is usually not germinating. Understand the germinating property as describing a seed. The printed name is "experimental soybean seed". The description is "I can't tell anything about this seed until I give it a chance to grow.".

The hardy drought-resistant soybean seed is a seed. It is nowhere. The adult form is the hardy drought-resistant soybean plant. Understand "experimental/-- soybean/-- seed" and "experimental/-- soybean" as the hardy drought-resistant soybean seed.
The hardy high-yield soybean seed is a seed. It is nowhere. The adult form is the hardy high-yield soybean plant. Understand "experimental/-- soybean/-- seed" and "experimental/-- soybean" as the hardy high-yield soybean seed.
The drought-resistant high-yield soybean seed is a seed. It is nowhere. The adult form is the drought-resistant high-yield soybean plant. Understand "experimental/-- soybean/-- seed" and "experimental/-- soybean" as the drought-resistant high-yield soybean seed.
The hardy drought-resistant high-yield soybean seed is a seed. It is nowhere. The adult form is the hardy drought-resistant high-yield soybean plant. Understand "experimental/-- soybean/-- seed" and "experimental/-- soybean" as the hardy drought-resistant high-yield soybean seed.
The nonviable soybean seed is a seed. It is nowhere. The adult form is the sickly soybean plant. Understand "experimental/-- soybean/-- seed" and "experimental/-- soybean" as the nonviable soybean seed.
The dead soybean seed is a seed. It is nowhere. The adult form is the sickly soybean plant. Understand "experimental/-- soybean/-- seed" and "experimental/-- soybean" as the dead soybean seed.


Planting is an action applying to one touchable thing. Understand "plant [something]" and "plant [something] in incubator" as planting.
Understand "plant [something] in/into [booth]" as inserting it into.
Instead of inserting or putting when the noun is a seed or the noun is a plant and the second noun is the incubator or the second noun is a booth:
	try planting the noun.
Instead of planting:
	if noun is not a seed and noun is not a plant:
		say "I can't plant that.";
		rule fails;
	otherwise if location is the Homebase:
		say "It's not the right time to plant something in the garden.";
		rule fails;
	otherwise if location is the Laboratory:
		let available-booth be first-empty-slot;
		if first-empty-slot is slot 0:
			say "No slots are available for planting.";
			rule fails;
		otherwise:
			say "I plant the [noun] in [available-booth] of the incubator. Now I just need to give it time to grow. I should examine it tomorrow. I quickly add some information about it in my log.";
			move noun to available-booth;
			now noun is planted;
			if available-booth is open:
				try closing available-booth;
			rule succeeds;
	otherwise:
		say "I can't plant anything here.";
		rule fails.

[mix-stuff]
To mix-stuff (ingredients - a list of objects):
	sort ingredients in printed name order;
	if ingredients is {baking powder, flour, salt, sugar, water}:
		try making pancake;
	otherwise if ingredients is {flour, salt, water}:
		try making hardtack;
	otherwise if ingredients is {borax, sugar, water}:
		try making ant poison;
	otherwise if ingredients is {borax, sugar} and ants are on-stage:
		say "This seems close but won't work; it needs something else in addition. The ants would just pick out the sugar.";
		rule fails;
	otherwise if ingredients is {borax, water} and ants are on-stage:
		say "This doesn't seem right but is probably pretty close; it needs something else in addition. There's nothing to attract the ants to it.";
		rule fails;
	otherwise if ingredients is {sugar, water} and ants are on-stage:
		say "I decide against this course of action because deliberately feeding the ants would only make things worse.";
		rule fails;
	otherwise:
		let all-plants be true;
		let all-slots be true;
		repeat with ingredient running through ingredients:
			if ingredient is not an experiment:
				now all-plants is false;
			if ingredient is not a booth:
				now all-slots is false;
		if all-plants is true or all-slots is true:
			instead say "I can only combine two plants at once.[if player is not in Laboratory] Plus, I can only combine plants in the laboratory with the genetic plant splicer.[end if]";
		otherwise:
			instead say "That doesn't seem useful.";
	rule fails;
	stop the action.

Mixing is an action applying to two visible things. Understand "mix [something preferably held] with/and [something preferably held]" and "combine [something] with/and [something]" and "pollinate [something] with [something]" and "cross-pollinate [something] with [something]" and "cross [something] with/and [something]" and "crossbreed [something] with/and [something]" and "cross-breed [something] with/and [something]" and "hybridize [something] with/and [something]" and "splice [something] with/and [something]" as mixing.

Before mixing:
	if the canteen is touchable:
		now the canteen is open.
Carry out mixing:
	let new-noun be the noun;
	let new-second-noun be the second noun;
	if noun is a booth and something (called the current-item) is in the noun:
		now new-noun is current-item;
	if second noun is a booth and something (called the current-item) is in the second noun:
		now new-second-noun is current-item;
	if new-noun is a seed or new-second-noun is a seed:
		say "The genetic plant splicer won't work with seeds.[if player is not in Laboratory] Plus, the splicer is in the laboratory, not here.[end if]";
		rule fails;
	otherwise if new-noun is a plant and new-second-noun is a plant:
		if splicer-run-today-flag is 1:
			say "The gene splicer can only be run once a day.";
			rule fails;
		otherwise if any on-stage seed is not germinating:
			say "Per my procedure I can't process two non-germinating seeds at once. It would be too easy to confuse them and spoil my results.";
			rule fails;
		otherwise:
			say "After several hours of careful work with the genetic plant splicer, I have obtained a seed for a new variety of soybean. Of course I won't know if it's actually viable until I plant it in the incubator and give it time to sprout.";
			advance-time 322 minutes;
			let the resultant-seed be the nonviable soybean seed;
			let plants-to-be-crossed be a list of plants;
			add new-noun to plants-to-be-crossed;
			add new-second-noun to plants-to-be-crossed;
			sort plants-to-be-crossed in printed name order;
			if plants-to-be-crossed is {drought-resistant soybean plant, hardy soybean plant}:
				now the resultant-seed is the hardy drought-resistant soybean seed;
			otherwise if plants-to-be-crossed is {hardy soybean plant, high-yield soybean plant}:
				now the resultant-seed is the hardy high-yield soybean seed;
			otherwise if plants-to-be-crossed is {drought-resistant soybean plant, high-yield soybean plant}:
				now the resultant-seed is the drought-resistant high-yield soybean seed;
			otherwise if plants-to-be-crossed is {hardy drought-resistant soybean plant, high-yield soybean plant}:
				now the resultant-seed is the hardy drought-resistant high-yield soybean seed;
			if the resultant-seed is on-stage:
				now the resultant-seed is the dead soybean seed;
			move resultant-seed to player;
			now splicer-run-today-flag is 1;
			rule succeeds;
	otherwise:
		let ingredients be a list of objects;
		add the noun to ingredients;
		add the second noun to ingredients;
		mix-stuff ingredients.

Multiply-mixing is an action applying to touchable things. Understand "mix [things]" and "combine [things]" as multiply-mixing.

Before multiply-mixing:
	if the canteen is touchable:
		now the canteen is open.
Carry out multiply-mixing:
	Let ingredients be the multiple object list;
	repeat with item running through ingredients:
		alter the multiple object list to {};
	mix-stuff ingredients.

The selectively announce items from multiple object lists rule is listed instead of the announce items from multiple object lists rule in the action-processing rules.

This is the selectively announce items from multiple object lists rule:
	if not multiply-mixing:
		if the current item from the multiple object list is not nothing:
			say "[current item from the multiple object list]: [run paragraph on]".


Making is an action applying to one thing. Understand "make [any thing]" and "cook [any thing]" as making.
Before making:
	if the noun is a pancake:
		if canteen is touchable:
			now canteen is open;
		if the player is in the Hut:
			if the pancake is not nowhere:
				say "There's no point in making another one until after I eat this. It's healthier to eat fresh.";
				rule fails;
			otherwise if something is on the mini-stove:
				say "It isn't safe to try and make something when there's already something sitting on the stove.";
				rule fails;
			otherwise if flour is visible and percent full of flour > 0 and sugar is visible and percent full of sugar > 0 and salt is visible and percent full of salt > 0 and baking powder is visible and percent full of baking powder > 0 and water is visible:
				say "I mix up some flour[if ants are on-stage] (carefully avoiding ants)[end if], water, sugar[if ants are on-stage] (carefully avoiding ants)[end if], salt, and baking powder on the stove surface. After a little cooking time, I've successfully made a single large pancake. It takes up the full surface of the mini-stove and looks kind of funny, but it's light and fluffy and contains calories.";
				check-canteen-level;
				move pancake to player;
				decrease the percent full of flour by 10;
				decrease the percent full of sugar by 5;
				decrease the percent full of salt by 2;
				decrease the percent full of baking powder by 2;
				advance-time 34 minutes;
				if the percent full of sugar < 0:
					say "It looks like I finished the sugar. It'll be hardtack until the crop in the garden is ready.";
				rule succeeds;
			otherwise:
				say "I don't have everything I need here to make a pancake. I need at least flour, sugar, baking powder, salt, and water.";
				rule fails;
		otherwise:
			say "I can't make a pancake here. I need the mini-stove.";
			rule fails;
	otherwise if the noun is hardtack:
		if canteen is touchable:
			now canteen is open;
		if the player is in the Hut:
			if the hardtack is not nowhere:
				say "Even with hardtack it's better to make it as I eat it. It's healthier to eat fresh.";
				rule fails;
			otherwise if something is on the mini-stove:
				say "It isn't safe to try and make something when there's already something sitting on the stove.";
				rule fails;
			otherwise if flour is visible and percent full of flour > 0 and salt is visible and percent full of salt > 0 and some water is visible:
				say "I mix up some flour[if ants are on-stage] (carefully avoiding ants)[end if], salt, and water on the stove surface. After a longish cooking time, I've successfully made some hardtack. It's better than starvation.";
				check-canteen-level;
				move hardtack to player;
				decrease the percent full of flour by 10;
				decrease the percent full of salt by 8;
				advance-time 59 minutes;
				rule succeeds;
			otherwise:
				say "I don't have everything I need here to make hardtack. I need at least flour, water, and salt.";
				rule fails;
		otherwise:
			say "I can't make hardtack here. I need the mini-stove.";
			rule fails;
	otherwise if the noun is ant poison:
		if canteen is touchable:
			now canteen is open;
		if ant poison is not nowhere:
			say "I have enough already.";
			rule fails;
		otherwise if borax is visible and percent full of borax > 0 and sugar is visible and percent full of sugar > 0 and some water is visible:
			say "I mix up minuscule amounts of borax, sugar[if ants are on-stage] (carefully avoiding ants)[end if], and water and it has produced a small amount of cloudy, extremely viscous liquid.";
			move ant poison to player;
			decrease the percent full of sugar by 1;
			decrease the percent full of borax by 1;
			advance-time 9 minutes;
			rule succeeds;
	otherwise:
		say "I can't make that.";
		rule fails.

Instead of going up when the location is the Laboratory:
	try exiting.
Instead of exiting when the location is the Laboratory:
	say "I exit the lab into the outer compound.";
	move the player to the Homebase.
Before going a direction (called the way) when the location is the Laboratory and the way is not up:
	try exiting.

The Hut is a room. It is in the interior spaces. The description is "Home sweet hut! It's barely big enough to hold my sleeping cot on one side and my mini-stove, sink, and cabinet on the other. It keeps the weather out and I spend a lot more time in the lab anyway. Going down a few steps leads to the outer compound.".

The steps are scenery in the Hut. Understand "steps" and "few steps" as the steps. The description is "The steps lead down to the outer compound."

Rule for printing the description of a dark room when the location is the Hut:
	say "It is pitch dark, and I can't see a thing, but I know my cot is nearby.[if time of day < 4:00 AM] I need to get some sleep anyway.[end if]" instead.

The cot is an enterable supporter in the Hut. It is scenery. Understand "sleeping/-- cot" as the cot. The description is "Although it is not particularly comfortable, it definitely beats sleeping on the floor.".

The mini-stove is a supporter in the Hut. It is scenery. Understand "ministove", "mini-stove", "stove", "stove/-- surface" as the mini-stove. "I wish I had some kind of oven, but this little stove does enable me to cook some kinds of food with varying levels of success. The whole top surface is non-stick, so I usually just prepare food directly on it.".

Check switching on the mini-stove:
	say "I don't want to waste any resources. I'll turn the mini-stove on and off exactly when I need to as part of making something." instead.

The sink is a supporter in the Hut. It is scenery. Understand "small/dry/-- sink" as the sink. The description is "It is small and dry and looks hardly used.".

Check switching on the sink:
	say "I wish the sink worked. I wish I had some kind of running water rather than having to walk down to the water hole every day." instead.

The cabinet is a closed, openable container in the Hut. It is scenery. The description is "Almost as tall as me, the cabinet is slightly beaten up[if ants are on-stage] (and clearly not ant-proof)[end if] but able to generally hold and organize things.[if ants are on-stage] There are ants moving in and out of the cabinet.[end if] It is currently [if closed]closed[otherwise]open. There's a note pasted inside the door[end if].".

The note is part of the cabinet. Understand "cabinet note/door", "list/-- of/-- recipes", "recipes list/note", and "note" as the note. The description is "The note is a short list of handwritten recipes I've collected. I don't have them memorized so I'd have to read it to learn more.". The printing is "Glancing through, there are currently just two recipes recorded here that I might be able to make given my available equipment and ingredients. The first is pancakes: it needs flour, water, sugar, baking powder, and salt. The second is hardtack: it only needs flour and water. It'll be kind of a stretch making it on the mini-stove rather than in an oven, but I think I can probably make it work. That looks like it; probably I can make pancakes and probably I can make hardtack.".

Instead of doing something to the note when the cabinet is closed:
	say "I can't see any such thing.".

After listing contents while opening cabinet: say ". There is a note pasted to the inside of the door[run paragraph on]".

The flour is a large canister in the cabinet. Understand "wheat/-- flour" as the flour. The indefinite article is "some". The description of it is "A large open canister of wheat flour. [canister-level flour][if ants are on-stage][paragraph break]At the moment it's swarming with ants. Whoever said flour was poisonous to ants must never have actually observed their behavior around flour.[end if]". The percent full of flour is 75.

The sugar is a medium canister in the cabinet. The indefinite article is "some". The description of it is "A medium open canister of sugar. [canister-level sugar][if ants are on-stage][paragraph break]At the moment it's swarming with ants.[end if]". The percent full of sugar is 30.

Instead of taking when (the noun is the flour or the noun is the sugar):
	if the ants are on-stage:
		say "[ant-attack][paragraph break]I can't. That's swarming with ants right now.";
	otherwise:
		say "The [noun] is too heavy to carry around all the time. I only ever need it in the hut here anyway.";
	rule fails.

The salt is a small canister in the cabinet. The indefinite article is "some". The description of it is "A small open canister of salt. [canister-level salt]". The percent full of the salt is 97.

The baking powder is a small canister in the cabinet. The indefinite article is "some". The description of it is "A small open canister of baking powder. [canister-level baking powder]". The percent full of the baking powder is 90.

A canteen is a closed, openable container in the cabinet. The description is "Capable of holding a full quart of water, this canteen has been one of my most useful pieces of equipment. It is currently [if canteen is full]full[otherwise if canteen is three-quarters full]three-quarters full[otherwise if canteen is half-full]half-full[otherwise if canteen is one-quarter full]one-quarter full[otherwise]empty[end if].". The canteen can be full, three-quarters full, half-full, one-quarter full, or empty. The canteen is empty.

Rule for printing inventory details of canteen: stop.
Rule for printing room description details of canteen: stop.

To check-canteen-level:
	if canteen is full:
		now canteen is three-quarters full;
	otherwise if canteen is three-quarters full:
		now canteen is half-full;
	otherwise if canteen is half-full:
		now canteen is one-quarter full;
	otherwise:
		now canteen is empty;
		move water to the Oasis.

Instead of opening or closing when the noun is the canteen:
	say "I will do so automatically as needed. I don't need to do that now.";
	rule fails.

Rule for supplying a missing noun while inserting into the canteen:
	now the noun is water.
Instead of drinking when the canteen is touchable:
	If the canteen is not held:
		try taking the canteen;
	now the canteen is open;
	If water is in the canteen:
		advance-time 4 minutes;
		say "I drink from the canteen and quench my thirst.";
		check-canteen-level;
		decrease thirst of player by 25;
		now the canteen is closed;
		rule succeeds;
	otherwise:
		say "Unfortunately the canteen is empty. I'll have to refill it first.";
		rule fails;
	if thirst of player > 0:
		say "I still feel thirsty.".

Rule for supplying a missing noun while drinking and the canteen is visible (this is the default drinking rule):
	now the noun is the canteen.
Before drinking the water:
	if the canteen is touchable:
		now the canteen is open.
Instead of drinking the spring:
	if the canteen is touchable:
		try drinking canteen;
	otherwise:
		try drinking water.
Instead of drinking the canteen:
	now the canteen is open;
	try drinking water.

Instead of putting when the second noun is the canteen:
	try inserting noun into canteen.

Before inserting into when the the second noun is the canteen:
	if the noun is not water:
		say "I can only fill the canteen with water. I can't afford to mess it up.";
		rule fails;
	otherwise if the location is a Oasis:
		if the canteen is carried:
			if the canteen is full:
				say "The canteen is already full.";
				rule fails;
			say "I fill up the canteen.[if xeno is -2 and yako is 0 and water-tested-today-flag is 0][paragraph break]I still haven't tested the water today. I should do so.[end if]";
			advance-time 4 minutes;
			move some water to the canteen;
			now canteen is full;
			now water-tested-today-flag is 1;
			now have-filled-canteen is 1;
			now the canteen is closed;
			rule succeeds;
		otherwise:
			say "I don't have the canteen.";
			rule fails;
	otherwise:
		say "I can't do that here.";
		rule fails;

A recipe is a kind of thing. It is edible.
A pancake is a recipe. Understand "large/light/fluffy/-- pancake" as pancake. Understand "pancakes" as the plural of pancake. It is nowhere. The description is "large and light and fluffy, the pancake is a meal and a half.".
Does the player mean eating the pancake: it is very likely.
Some hardtack is a recipe. Understand "large/tooth-shattering/-- hardtack" as hardtack. It is nowhere. The description is "Large and tooth-shattering, the hardtack is best eaten with water to soften it, and even then it's neither fast nor pleasant to do so.".
Does the player mean eating the hardtack: it is very likely.

The ants are a plural-named animal. Understand "line/-- of/-- red/brown/reddish-brown/-- ants" and  "red/brown/reddish-brown/-- ant" as the ants. They are nowhere. The description is "Individually they are a reddish-brown in color and a little over half an inch long. Unfortunately there is not just one: there are hundreds.".

To say ant-attack:
	increase number-of-ant-bites by 1;
	increase sickness of player by 1;
	say "Ouch! One bit me or stung me or something! [if number-of-ant-bites is 1]That hurt more than I would expect it should. It's possible these things are venomous;[otherwise]I've already been stung [number-of-ant-bites in words] times.[end if] I should try to avoid getting bitten again."

Understand "stomp [something]" as attacking.
Instead of attacking the ants:
	say "I try to stomp them. I think I kill a few, but it's hard to tell as the hundreds of survivors quickly carry away the bodies.[paragraph break][ant-attack]";
	rule succeeds.

At the time when the ant poison works:
	remove the ants from play;
	if location is the Hut:
		say "It looks like the ant poison worked.".

Instead of giving or putting or inserting when the ants are on-stage and the noun is the borax and location is the Hut:
	try dropping the borax.

Instead of dropping when the ants are on-stage and location is the Hut and the noun is the borax:
	say "That's a good idea; borax is definitely poison to ants, but there's no reason for them to eat it.";
	rule fails.

Instead of giving or putting or inserting when the ants are on-stage and the noun is the ant poison and location is the Hut:
	try dropping the ant poison.

Instead of dropping when the ants are on-stage and location is the Hut and the noun is the ant poison:
	say "I carefully place the ant poison where I believe they'll find it. They do so almost immediately and quickly take it all. I know its effects can take awhile, though, so now I just have to wait. Hopefully they'll be gone tomorrow.";
	remove ant poison from play;
	the ant poison works in 12 hours from now;
	advance-time 10 minutes.

Instead of eating:
	if the noun is not held and the noun is touchable:
		try taking the noun;
	if the noun is a pancake:
		say "That hits the spot. That was one big pancake. [first time]If I can keep eating one of these per day, I'll be fine.[only]";
		decrease hunger of player by 50;
		now the pancake is nowhere;
		advance-time 29 minutes;
		rule succeeds;
	otherwise if the noun is hardtack:
		if water is in canteen:
			say "I feel less hungry now. Water definitely helps the hardtack go down more easily. [first time]Eating one of these per day feels like almost enough.[only]";
			decrease hunger of player by 45;
			decrease thirst of player by 10;
			check-canteen-level;
			advance-time 39 minutes;
		otherwise:
			say "I feel less hungry now. It's a shame though I don't have any water to go with this hardtack. It makes eating it a lot easier. [first time]Eating one of these per day doesn't feel like quite enough.[only]";
			decrease hunger of player by 40;
			increase thirst of player by 5;
			advance-time 59 minutes;
		now the hardtack is nowhere;
		rule succeeds;
	otherwise if the noun is a seed:
		say "I eat the soybean.";
		decrease hunger of player by 1;
		rule succeeds;
	otherwise if the noun is sugar:
		if percent full of sugar > 0:
			say "I wolf down some of the sugar before I realize what I'm doing. I feel a little sick.";
			decrease hunger of player by 10;
			increase sickness of player by 12;
			decrease percent full of sugar by 25;
			advance-time 9 minutes;
		otherwise:
			say "There is no sugar left.";
		rule fails;
	otherwise if the noun is flour:
		if percent full of flour > 0:
			say "I manage to eat some of the flour before I realize what I'm doing. Its dryness makes me cough and I feel a little sick.";
			decrease hunger of player by 10;
			increase sickness of player by 5;
			decrease percent full of flour by 15;
			advance-time 9 minutes;
		otherwise:
			say "There is no flour left.";
		rule fails;
	otherwise if the noun is salt:
		if percent full of salt > 0:
			say "I manage to eat some of the salt before I realize what I'm doing. I feel a little sick.";
			decrease hunger of player by 2;
			increase sickness of player by 5;
			decrease percent full of salt by 15;
			advance-time 9 minutes;
		otherwise:
			say "There is no salt left.";
		rule fails;
	otherwise if the noun is baking powder:
		if percent full of baking powder > 0:
			say "I manage to eat some of the baking powder before I realize what I'm doing. Its dryness makes me cough and I feel a little sick.";
			decrease hunger of player by 1;
			increase sickness of player by 10;
			decrease percent full of baking powder by 15;
			advance-time 9 minutes;
		otherwise:
			say "There is no baking powder left.";
		rule fails;
	otherwise if the noun is borax:
		if percent full of borax > 0:
			say "I somehow manage to eat some of the borax before I realize what I'm doing. Its dryness makes me cough and I feel sick.";
			decrease hunger of player by 2;
			increase sickness of player by 40;
			decrease percent full of borax by 15;
			advance-time 9 minutes;
		otherwise:
			say "There is no borax left.";
		rule fails;
	otherwise if the noun is ant poison:
		say "I eat the ant poison before I realize what I'm doing. I feel sick.";
		decrease hunger of player by 1;
		increase sickness of player by 40;
		remove ant poison from play;
		advance-time 4 minutes;
		rule succeeds;
	otherwise:
		say "Even with my brain a little fuzzy I know I can't eat that.";
		rule fails;
	if hunger of player > 0:
		say "I still feel hungry.".


Instead of going down when the location is the Hut:
	try exiting.
Instead of exiting when the location is the Hut:
	say "I exit the hut into the outer compound.";
	move the player to the Homebase.
Before going a direction (called the way) when the location is the Hut and the way is not down:
	try exiting.

The Homebase is a room. It is in the exterior spaces. "It's not much, but it's home. My base consists of a small below ground lab where I work, a smaller above ground hut where I eat and sleep, and a small garden in between. The entirety is enclosed in a [first time]9' electric [only]fence[first time] to keep Them out[only]. Besides being able to go down to my lab and up to my hut, my main source of water is to the west. In all other directions is just the wasteland. "

Rule for printing the description of a dark room when the location is the Homebase:
	say "It is pitch dark, and I can't see a thing, but I know my hut and lab are both nearby." instead.

The fence is scenery in the Homebase. Understand "electric/-- fence" and "gate" as the fence. The description is "The fence surrounds and protects my entire base. It is about 9' tall and electrified with the ability to dish out several powerful shocks each night. Usually a single shock is enough to discourage Them.[first time] That's really where most of the electricity I generate from the roof panels goes.[only] I can easily get in and out via a gate[first time], but They cannot[only].".

Instead of touching the fence:
	say "Not even I can touch the fence without getting badly shocked.".

After deciding the scope of the player while in darkness:
	if the location is the Homebase:
		place the house in scope;
		place the lab in scope;
	otherwise if the location is the Hut:
		place the cot in scope;
		place the location in scope;
	otherwise if the location is the Laboratory:
		place the location in scope.

The garden is scenery in the Homebase. Understand "small/-- garden/crop" and "garden/-- reservoir" as the garden. The description is "It is currently in between crops. In a week or two I should have some tomatoes, some Swiss chard, and even some wheat (my little hand-powered grain mill is just behind the garden next to my rain barrel). I'll have to make do with what I've got stored already in my cabinet though until this next crop is ready. I have it set up so that I can fill a special reservoir once a day to keep it watered.[if days-since-garden-filled > 0] In fact, I still have to fill it up today.[otherwise if days-since-garden-filled > 1] In fact, the plants are looking a little rough and I should water them soon. [otherwise if days-since-garden-filled > 2] It looks like I somehow must have been forgetting to water them; they don't look like they're doing well.[otherwise if days-since-garden-filled > 3] I really need to water them now! I don't know if they'll survive another night.[end if][if a random chance of 1 in 3 succeeds] I glance at the stone marker in the corner.[end if]"

Before inserting into when the the second noun is the garden:
	if the noun is water or the noun is the canteen:
		if the location is not the Homebase:
			say "The garden is not here." instead;
			rule fails;
		otherwise if the canteen is not touchable:
			say "I need the canteen to fill the garden reservoir with water." instead;
			rule fails;
		otherwise if water is not in the canteen:
			say "There is no water in the canteen." instead;
			rule fails;
		otherwise if canteen is half-full or the canteen is one-quarter full:
			say "There is not enough water in the canteen. The garden needs a full quart." instead;
			rule fails;
		otherwise:
			say "I fill the garden reservoir with water from the canteen.";
			move the water to the Oasis;
			now canteen is empty;
			now days-since-garden-filled is 0;
			rule succeeds;

The vegetables are part of the garden. Understand "tomato", "tomatoes", "vegetable", "vegetables", "swiss chard", "chard", "grain", and "wheat" as vegetables.

Instead of doing something to vegetables:
	say "I have to leave them alone for now. I can't jeopardize the yield of this crop.".

The mill is scenery in the Homebase. Understand "hand-powered/-- grain mill" and "hand-powered/-- mill" as the mill. The description is "I'll use the grain mill to make more flour after I've harvested the wheat.".

The barrel is scenery in the Homebase. It is an open, unopenable container. Understand "rain/-- barrel" and "rainbarrel" as the barrel. The description is "It hasn't rained for awhile and the rain barrel is completely empty. Life is a lot easier when it is full.".

The stone marker is part of the garden. The description of it is "[if thought-number < 35]I don't want to think about that right now[otherwise]I choke up a little when I glance at Fantastic's marker[end if].".

The lab is a closed, openable, enterable container in the Homebase. It is scenery. Understand "laboratory" and "lab" as the lab.

Instead of entering when the location is the Homebase and the noun is the lab:
	try going down.
	
Instead of going down when the location is the Homebase:
	say "I enter the laboratory.";
	move the player to the Laboratory.

The house is a closed, openable, enterable container in the Homebase. It is scenery. Understand "house" and "hut" as the house.

Instead of entering when the location is the Homebase and the noun is the house:
	try going up.

Instead of going up when the location is the Homebase:
	say "I enter the hut.";
	move the player to the Hut.

To say nearby-landmarks:
	if xeno is -1:
		if yako is 1:
			say ". Fortunately from here I can barely make out the home base to the southeast";
		otherwise if yako is -1:
			say ". Fortunately from here I can barely make out the home base to the northeast";
	otherwise if xeno is 0:
		if yako is 1:
			say  ". Fortunately from here I can barely make out the home base to the south";
		otherwise if yako is -1:
			say ". Fortunately from here I can barely make out the home base to the north";
	if xeno is 1:
		if yako is 1:
			say ". Fortunately from here I can barely make out the home base to the southwest";
		otherwise if yako is 0:
			say ". Fortunately from here I can barely make out the home base to the west";
		otherwise if yako is -1:
			say ". Fortunately from here I can barely make out the home base to the northwest".

The Wasteland is a room. It is in the dangerous spaces. "The wasteland seems to go on forever in all directions. It's desolate and unforgiving, and easy to get lost[nearby-landmarks].[if thought-number <= number-of-thoughts][paragraph break][italic type][entry thought-number of thoughts][roman type][end if]"

The Path is a room. It is in the dangerous spaces. "The vague path runs toward the base in the east and the oasis in the west. It's only barely recognizable from the rest of the wasteland."

The Meadow is a room. "It's a beautiful sight for wasteland-weary eyes: a small meadow of robust plants somehow bursting up through the hard, dead soil and literally bearing fruit. They're lowbush blueberry plants that have managed to adapt themselves to the wasteland and are slowly spreading on their own. There is enough here both to cultivate all around my home base and sustain me while I'm waiting for my other crops! Life has found a way!"

The Oasis is a room. It is in the dangerous spaces. "Although the wasteland seems to go on forever in all directions, this little patch of it contains a muddy puddle resulting from an underground spring.[first time] Calling it an oasis makes it seem a lot nicer and more exotic than it is.[only][if xeno is -2 and yako is 0] The start of a path to the base is to the east.[end if][first time][paragraph break]I test the water quality daily. If it ever went bad I'd need to find an alternative source pretty quickly.[only]"

After deciding the scope of the player while the location is the Oasis or (the player has the canteen and the water is in the canteen):
	now the have-filled-water-flag is 1;
	place the water in scope.

The spring is scenery in the Oasis. It is a transparent, enterable container. Understand "muddy/-- puddle", "muddy/-- spring", "muddy/-- water" as the spring. The description is "The spring is a few feet across and a little bit muddy. Although the water does not look ideal, I remind myself that I do test it daily and it is potable and reasonably safe.[if xeno is -2 and yako is 0 and water-tested-today-flag is 0] Actually now that I think about it, I still need to test it today.[end if]".

Instead of inserting or putting when the second noun is the spring or the second noun is the water:
	if the noun is the canteen:
		try inserting water into the canteen;
	otherwise:
		try entering the spring.

Instead of entering when the noun is the spring or the noun is the water:
	say "I can't risk corrupting my water supply.".

The water is scenery in the spring. The indefinite article is "some". The description is "It's water. It's nearly completely transparent, and that's not bad!".
Understand "drink from" as drinking.
Does the player mean drinking canteen: it is very likely.
Does the player mean drinking water: it is likely.
Instead of taking the spring:
	try taking water.
Instead of taking water:
	if player has the canteen:
		try inserting water into the canteen;
	otherwise:
		say "I don't have anything to hold the water.";
		rule fails.
Instead of dropping water:
	say "Water is too precious to waste.";
	rule fails.

To check-safety-distance:
	let time-before-dusk be 7:30 PM minus the time of day;
	let time-to-get-back be rooms-away * 10 minutes;
	if time-to-get-back is time-before-dusk:
		say "I need to get back. If I retrace my steps exactly I should get back to the base before They come out.";
	otherwise if time-to-get-back > time-before-dusk:
		say "I really [italic type]really[roman type] need to get back. I don't know if I'm going to make it in time.".

To check-for-noises:
	if Night is happening:
		if a random number from 1 to 23 is 1:
			say "I don't even hear Them before they're upon me. I'm taken completely by surprise.";
			end the story saying "They Win";
	otherwise:
		if a random number from 1 to 50 is 1:
			say "I hear some scrabbling in the distance[first time]. It should be okay for now, though; They can't tolerate the light and so can't come above ground for more than a few seconds during the day[only].".

Table of Oasis Locations
Oasis Xeno	Oasis Yako
-2			0
-5			1
2			3
-3			-2
1			-4
5			5

Locations table is a table name that varies. Locations table is Table of Oasis Locations.

To place-the-player (way - direction):
	advance-time 9 minutes;
	if xeno is 0 and yako  is 0:
		say "I find my way back to the base.";
		move the player to the Homebase;
		now rooms-away is 0;
		rule succeeds;
	otherwise if xeno is -1 and yako is 0:
		if old_yako is not 0 or old_xeno > 0 or old_xeno < -2:
			say "I find my way to the path.";
		move the player to the Path;
		now rooms-away is 1;
		rule succeeds;
	otherwise if xeno is 5 and yako is 23:
		say "I've made an astounding discovery!";
		move the player to the Meadow;
		end the story finally saying "You Win";
		rule succeeds;
	otherwise:
		repeat through locations table:
			if xeno is the Oasis Xeno entry and yako is the Oasis Yako entry:
				if xeno is -2 and yako is 0:
					if old_xeno is not -1 and old_yako is not 0:
						say "I find my way to my regular source of water.";
						now rooms-away is 2;
				otherwise:
					now alternate-water-flag is 1;
					say "This looks like it could serve as an alternative water source!";
				move the player to a Oasis;
				increase rooms-away by 1;
				check-safety-distance;
				check-for-noises;
				rule succeeds;
	say "I wander [way] for a time.";
	increase thought-number by 1;
	increase rooms-away by 1;
	move the player to the Wasteland;
	check-safety-distance;
	check-for-noises;
	rule succeeds.

Instead of going north:
	now old_yako is yako;
	increase yako by 1;
	place-the-player (north).

Instead of going south:
	now old_yako is yako;
	decrease yako by 1;
	place-the-player (south).

Instead of going east:
	now old_xeno is xeno;
	increase xeno by 1;
	place-the-player (east).

Instead of going west:
	now old_xeno is xeno;
	decrease xeno by 1;
	place-the-player (west).

Instead of going northeast:
	now old_xeno is xeno;
	now old_yako is yako;
	increase xeno by 1;
	increase yako by 1;
	place-the-player (northeast).

Instead of going northwest:
	now old_xeno is xeno;
	now old_yako is yako;
	decrease xeno by 1;
	increase yako by 1;
	place-the-player (northwest).

Instead of going southeast:
	now old_xeno is xeno;
	now old_yako is yako;
	increase xeno by 1;
	decrease yako by 1;
	place-the-player (southeast).

Instead of going southwest:
	now old_xeno is xeno;
	now old_yako is yako;
	decrease xeno by 1;
	decrease yako by 1;
	place-the-player (southwest).

Before waiting:
	advance-time 14 minutes.

Waiting more is an action applying to one number.

Understand "wait [a time period]" or "wait for [a time period]" or "wait for a/an [a time period]" or "wait a/an [a time period]" as waiting more.

Carry out waiting more:
	advance-time the time understood.

Report waiting more:
	say "I patiently wait until [time of day + 1 minute]."

To say player-status:
	let status be 0;
	if sickness of player > 0:
		decrease sickness of player by 3;
		say "I wake up feeling a little less sick now than before sleeping[if sickness of player > 0], but still not right[end if].";
		now status is 1;
	if hunger of player > 101:
		say "I wake up feeling like I haven't eaten in days. I need to eat soon.";
		now status is 1;
	otherwise if hunger of player > 51:
		say "I wake up feeling really hungry. I need to eat soon.";
		now status is 1;
	if thirst of player > 101:
		say "I wake up feeling so thirsty I think it's messing me up. If I don't get something to drink soon I may die.";
		now status is 1;
	otherwise if thirst of player > 51:
		say "I wake up feeling really thirsty. I need to drink soon.";
		now status is 1;
	if status is 0:
		say "I wake up feeling okay.".

To say night-sounds:
	say "[one of]It's a quiet night[first time]; there's just the sound of a lone coyote. Heh, [italic type]Coyote Waits[roman type] I guess[only].[or]I hear some crashes of thunder during the night. It's just a dry thunderstorm, though. There's no rain.[or]I hear Them scrabbling around outside the base. It don't hear any touch the fence, though; They seem to remember.[as decreasingly likely outcomes]".

Sleeping more is an action applying to one number.

Understand "sleep [a time period]" or "sleep for [a time period]" or "sleep for a/an [a time period]" or "sleep a/an [a time period]" or "nap [a time period]" or "nap for [a time period]" or "nap for a/an [a time period]" or "nap a/an [a time period]" as sleeping more.

Carry out sleeping more:
	if location is not Hut:
		say "This is not where I sleep.";
		rule fails;
	otherwise:
		if the player is not on the cot:
			try entering cot;
			if Night is happening and the time understood > 2 hours:
				say "[night-sounds]";
			advance-time the time understood;
			if the time understood > 5 hours:
				say "[player-status]".

Report sleeping more:
	say "I sleep until [time of day + 1 minute]."

Instead of sleeping:
	if location is not Hut:
		say "This is not where I sleep.";
		rule fails;
	otherwise:
		if the player is not on the cot:
			try entering cot;
		if Night is happening:
			say "[night-sounds]";
		advance-time eight hours;
		say "I sleep about eight hours.";
		say "[player-status]";
		rule succeeds;

Instead of smelling:
	say "I briefly start to sniff out of habit before remembering that I can no longer smell. I got anosmia when I was sick earlier and never recovered my sense of smell.".

Instead of tasting:
	say "I momentarily think about taste and wince a little when I recall that the virus took that ability away from me.".

Understand the command "read" as something new. Understand "read [something]" and "skim [something]" as reading. Reading is an action applying to one thing, requiring light.
Check reading: if the printing of the noun is "blank", say "Nothing is written on [the noun]." instead.
Carry out reading: say "[printing of the noun][line break]".
Report reading: do nothing.
After reading: advance-time 9 minutes.

Understand "fill up/-- [something] with [something]" as inserting it into (with nouns reversed).
Understand "refill [something] with [something]" as inserting it into (with nouns reversed).
Does the player mean inserting water into the canteen when the player carries the canteen and the location is the Oasis: it is very likely.
Does the player mean inserting the canteen into the canteen: it is very unlikely.
Does the player mean inserting water into the incubator when the player carries the canteen and the location is the Laboratory and water is in the canteen: it is very likely.
Does the player mean inserting water into the incubator when the player carries the canteen and the location is the Laboratory: it is likely.
Does the player mean inserting water into the garden when the player carries the canteen and the location is the Homebase and water is in the canteen: it is very likely.
Does the player mean inserting water into the garden when the player carries the canteen and the location is the Homebase: it is likely.

Instead of dropping in the exterior spaces when the location is not the Homebase:
	say "I can't leave anything here, because They will find it and weaponize it against me somehow.";
	rule fails.

Instead of examining the player:
	say "I'm glad I don't have a mirror around. Since I ran out of rainwater a week ago I've only been able to take a couple of 'powder baths'. Hopefully it'll rain again soon.".

Instead of thinking:
	say "My primary goal has to be to grow the right combination of soybean plants to result in something capable of growing in the wasteland and producing food. Each splicing experiment will take a big chunk of the day to complete so I should organize my day's activities around that[run paragraph on][if incubator-filled-today-flag is 0]. I also have to remember to fill the incubator basin with water so my experimental soybean plants will survive[end if].";
	if days-since-garden-filled > 0:
		say "I need to water my garden by filling its reservoir daily, otherwise those vegetables that I'm relying on won't be ready when I need them.";
	if water-tested-today-flag is 0:
		say "I need to test my water source today to ensure it's still safe[run paragraph on][if alternate-water-flag is 0]. I also need to locate a backup source of it, just in case some day it isn't, and that means exploring the nearby wasteland[end if].";
	otherwise if alternate-water-flag is 0:
		say "I need to locate a backup source of water in case some day my current water source fails.";
	if sickness of player > 0:
		say "I have to take better care of myself. I won't succeed if I am sick all the time.";
	if thirst of player > 0:
		say "I need to get enough to drink to stay hydrated every day.";
	if hunger of player > 0:
		say "I need to get enough to eat daily to stay in good shape. I can make something so long as I have the recipe, ingredients, and equipment.";

Fetching is an action applying to nothing. Understand "fetch" and "fetch water" as fetching.
Carry out fetching:
	if have-filled-water-flag is 1:
		if the canteen is touchable:
			try taking canteen;
			if xeno is not 0 or yako is not 0:
				say "The fetching shortcut only works from within the home base.";
			otherwise:
				say "I'm going to make my regular trip out for water. I know the routine by heart.";
				try going west;
				try going west;
				try inserting water into canteen;
				try going east;
				try going east;
		otherwise:
			say "The fetching shortcut only works if the canteen is already handy.";
	otherwise:
		say "The fetching shortcut only works [italic type]after[roman type] filling a canteen the hard way at least once.".
		
Abouting is an action applying to nothing. Understand "about" as abouting.
Carry out abouting:
	say "[italic type]Timeout in the Wasteland[roman type] is by Eric W. Brown, a.k.a. Feneric. It is part of the 2020 NarraScope Game Jam. It's a game of deliberately short scope. You'll have a maximum of five game day in which to try to complete your mission (if you succeed earlier the game will end sooner).[paragraph break]My thanks go out to Rhianon P. Brown, Caleb Wilson, and Brian Rushton for play testing this game. Their repeated efforts to both solve things and break things helped make it better.[paragraph break]If you've not played parser-based interactive fiction before (or you get stuck or you would just like to know about any special verb syntax used in this ) you may want to try typing 'hint'. While more details can be found there, a couple typing saving shortcuts will be mentioned here.[paragraph break]The first is that a direction can be entered from anywhere within the home base (including one of the buildings) to quickly leave it; it's not necessary to manually exit the building first. Once you know your way around a bit, this will save you time.[paragraph break]The second is that you never need to manually open or close the canteen (and in fact the command 'drink' will do what you want without you saying what) or the incubator.[paragraph break]Third, you can specify how long you want to sleep or wait if you wish to skip time waiting for sun up or something else to occur via a command like 'wait for 15 minutes' or 'sleep for 6 hours'.[paragraph break]Finally, once you have figured out how to get water, the command 'fetch water' issued from within your home base will go through the multiple steps of getting water and returning.".

Hinting is an action applying to nothing. Understand "hint" as hinting. Understand "help" as hinting.
Carry out hinting:
	say "So, you need to breed a soybean plant that can survive in the wasteland and produce enough food to ensure your future survival, and you have just a total of five days in which to do it. You also have to do a few other things along the way like keep yourself fed and keep your other plants alive. You probably knew that already. There are lots of things you can do in this game to help you achieve that goal. It understands lots of verbs. Besides the ubiquitous list of 'look' (l), 'examine' (x), 'inventory' (i), 'go north' (n), 'go south' (s), 'go east' (e), 'go west' (w) 'go northeast' (ne), 'go northwest' (nw), 'go southeast' (se), 'go southwest' (sw), 'go up' (u), 'go down' (d), 'again' (g) and the common list of 'attack', 'climb', 'drop', 'eat', 'enter', 'exit', 'fetch', 'get', 'listen', 'pick up', 'press', 'pull', 'push', 'read', 'restore', 'save', 'say', 'search', 'smell', 'taste', 'touch', 'undo', and 'wait', there are lots of other less common verbs like 'dig', 'fill', 'make', 'mix', 'plant', 'splice', 'sleep', 'sleep for', 'stomp', 'wait for' and others besides that may help.[paragraph break]Of especial note is that the syntax for both 'wait for' and 'sleep for' takes an amount of time; for example, one might 'sleep for 9 hours' or 'wait for 20 minutes'. Also please note that due to the way Inform works, it's possible to 'mix first thing and second thing and third thing' or 'mix first thing, second thing, third thing' or even 'mix first thing, second thing and third thing', but not 'mix first thing, second thing, and third thing'. Adherents of the Oxford comma beware.[paragraph break]If at any point you get really stuck, don't be afraid to sit back and 'think'.".


[Scenes]

Night is a recurring scene. Night begins when Study Time ends. Night ends when time of day is 5:15 AM.

When Night begins:
	if the player is in the interior spaces:
		say "The interior LEDs run out of charge and shut off. It'll now be dark until morning.";
	now the lights-on-flag is 0;
	now all the rooms in the interior spaces are dark;
	if day-count >= 5:
		say "Here alone in the darkness I realize I have failed. Maybe I will succeed tomorrow or the next day or the next, but it will likely be too late regardless.";
		end the story saying "You Lose".

Dawn is a recurring scene. Dawn begins when Night ends. Dawn ends when the time since Dawn began is 15 minutes.

When Dawn begins:
	if the player is in the exterior spaces:
		say "The sun appears on the horizon.";
	otherwise:
		say "It is getting light out.";
	now all rooms are lighted;
	increase hunger of player by 50;
	if hunger of player >= 100:
		say "I feel kind of weak. I don't think I've been eating enough.";
	increase thirst of player by 50;
	if thirst of player > 150:
		say "I feel awful. I'm so thirsty.";
		end the story saying "You Lose";
	otherwise if thirst of player >= 100:
		say "I don't feel good. I think I'm somewhat dehydrated.";
	if incubator-filled-today-flag is 1:
		repeat with growing-plant running through every planted plant:
			increase the days old of growing-plant by 1;
			if the growing-plant is not thriving:
				now growing-plant is not planted;
				now days old of growing-plant is 0;
				let current-booth be holder of growing-plant;
				let current-name be description of growing-plant;
				let current-description be description of growing-plant;
				remove growing-plant from play;
				if growing-plant is not sickly soybean plant:
					move sickly soybean plant to current-booth;
					now sickly soybean plant is planted;
					now printed name of sickly soybean plant is current-name;
					now description of sickly soybean plant is current-description;				
		repeat with growing-seed running through every planted seed:
			if days old of growing-seed is 1:
				now growing-seed is not planted;
				now days old of growing-seed is 0;
				let current-booth be holder of growing-seed;
				remove growing-seed from play;
				if growing-seed is not dead soybean seed:
					move adult form of growing-seed to current-booth;
					now adult form of growing-seed is planted;
			otherwise:
				now days old of growing-seed is 1;
				if growing-seed is dead soybean seed:
					now printed name of growing-seed is "nonviable experimental soybean seed";
				otherwise:
					now growing-seed is germinating;
					now printed name of growing-seed is "germinating experimental soybean seed";
	if day-count is 2:
		move ants to the Hut;
	if ants are on-stage:
		decrease the percent full of flour by 1;
		decrease the percent full of sugar by 3;
	now the water-tested-today-flag is 0;
	now incubator-filled-today-flag is 0;
	increase days-since-garden-filled by 1;
	if days-since-garden-filled > 4:
		now garden-dead-flag is 1;
		now description of garden is "This used to be my garden. It's all dead now.";
	now splicer-run-today-flag is 0;
	increase day-count by 1;
	now the description of the sun is "It is still partially below the horizon.".

Day is a recurring scene. Day begins when play begins. Day begins when Dawn ends. Day ends when the time of day is 7:40 PM.

When Day begins:
	if the player is in the exterior spaces:
		say "The sun has cleared the horizon.";
	now the description of the sun is "It is too bright to view directly."

Dusk is a recurring scene. Dusk begins when Day ends. Dusk ends when the time since Dusk began is 15 minutes.

When Dusk begins:
	if the player is in the exterior spaces:
		say "The sun is sinking below the horizon.";
	otherwise:
		say "It's gotten dark enough to cause the interior LEDs to flicker on and cast a dim light. They won't stay charged long. I should probably get ready for bed.";
	now the lights-on-flag is 1;
	now the description of the sun is "It is partially below the horizon.".

Study Time is a recurring scene. Study Time begins when Dusk ends. Study Time ends when the time since Study Time began is 90 minutes.

When Study Time begins:
	if the player is in the exterior spaces:
		say "The sun finally sinks completely beneath the horizon.";
	now all the rooms in the exterior spaces are dark;
	now the description of the sun is "It is nowhere to be seen."


[Testing]

Test make-pancake with "exit/enter house/open cabinet/get canteen/w/w/fill canteen/e/e/enter hut/mix water, flour, baking powder, salt, sugar/i".
Test make-hardtack with "exit/enter house/open cabinet/get canteen/w/w/fill canteen/e/e/enter house/mix water, flour and salt/i".
Test make-ant-poison with "get borax/exit/enter house/open cabinet/get canteen and sugar/w/w/fill canteen/mix water, sugar, and borax/i".
Test water-test with "get potability tester/exit/w/w/put tester in spring/turn on tester/get tester/put tester in water/get tester/turn off tester".
Test fill-incubator with "exit/enter hut/open cabinet/get canteen/w/w/fill canteen/e/e/enter lab/fill basin".
Test hardy-drought-resistant-plant with "splice slot 2 with slot 3/i/plant seed in incubator/x incubator".
Test hardy-high-yield-plant with "splice slot 2 with slot 1/i/plant seed in incubator/x incubator".
Test drought-resistant-high-yield-plant with "splice slot 3 with slot 1/i/plant seed in incubator/x incubator".
